@echo off
rem generic script to build a contrib with MSVC.
rem

setlocal

set LOCALCOMPILEOPT=/c /nologo /Fo"%INTPATH%" /Fd"%INTPATH%"

IF DEFINED PCH (
	%COMPILER% %LOCALCOMPILEOPT% %COMPILEOPTS% %PREPROCDEF% %PCHSRC% /Yc"%PCH%" /Fp"%INTPATH%%PROJNAME%.pch"
	IF ERRORLEVEL 1 goto :ERR
	set COMPILEOPTS=%COMPILEOPTS% /Yu"%PCH%" /Fp%INTPATH%%PROJNAME%.pch
)

%COMPILER% %LOCALCOMPILEOPT% %COMPILEOPTS% %PREPROCDEF% %SRCS%
IF ERRORLEVEL 1 goto :ERR

IF %STATIC% == 1 (
	%LINKER% %LINKERDEFOPTS% /OUT:"%LIBPATH%%LIBNAME%" "%INTPATH%*.obj" %LIBOPTS%
) ELSE (
	%LINKER% %LINKERDEFOPTS% /OUT:"%BINPATH%%DLLNAME%" "%INTPATH%*.obj" %LIBOPTS%
)
IF ERRORLEVEL 1 goto :ERR

goto :END

:ERR
endlocal
exit /b 1

:END
endlocal
